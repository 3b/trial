(in-package #:org.shirakumo.fraf.trial.examples)

(define-shader-entity gjk-body (vertex-entity colored-entity rigidbody listener)
  ((vertex-array :initform NIL)
   (color :initform (vec 0 0 1 0.5))))

(defmethod shared-initialize :after ((body gjk-body) slots &key primitive)
  (when primitive (setf (physics-primitive body) primitive)))

(defmethod physics-primitive ((body gjk-body))
  (aref (physics-primitives body) 0))

(defmethod (setf physics-primitive) ((primitive primitive) (body gjk-body))
  (setf (physics-primitives body) (vector primitive))
  (if (vertex-array body)
      (replace-vertex-data (vertex-array body) primitive)
      (setf (vertex-array body) (make-vertex-array primitive NIL))))

(define-handler (gjk-body pre-tick) ()
  (start-frame gjk-body))

(define-shader-entity gjk-player (gjk-body)
  ((hit :initform (make-hit) :accessor hit)
   (test-method :initform 'generic :accessor test-method)))

(defun detect-hit* (method a b &optional (hit (make-hit)))
  (let ((array (make-array 1))
        (a (physics-primitive a))
        (b (physics-primitive b)))
    (declare (dynamic-extent array))
    (setf (aref array 0) hit)
    (let ((count (ecase method
                   (generic (detect-hits a b array 0 1))
                   (gjk (org.shirakumo.fraf.trial.gjk:detect-hits a b array 0 1))
                   (v-clip (org.shirakumo.fraf.trial.v-clip:detect-hits a b array 0 1)))))
      (when (< 0 count)
        hit))))

(define-handler (gjk-player tick :after) (dt)
  (let ((spd (* 3.0 dt)))
    (cond ((retained :shift)
           (when (retained :a)
             (nq* (orientation gjk-player) (qfrom-angle +vy3+ (+ spd))))
           (when (retained :d)
             (nq* (orientation gjk-player) (qfrom-angle +vy3+ (- spd))))
           (when (retained :w)
             (nq* (orientation gjk-player) (qfrom-angle +vx3+ (+ spd))))
           (when (retained :s)
             (nq* (orientation gjk-player) (qfrom-angle +vx3+ (- spd))))
           (when (retained :c)
             (nq* (orientation gjk-player) (qfrom-angle +vz3+ (+ spd))))
           (when (retained :space)
             (nq* (orientation gjk-player) (qfrom-angle +vz3+ (- spd)))))
          (T
           (when (retained :a)
             (incf (vx (location gjk-player)) (- spd)))
           (when (retained :d)
             (incf (vx (location gjk-player)) (+ spd)))
           (when (retained :w)
             (incf (vz (location gjk-player)) (- spd)))
           (when (retained :s)
             (incf (vz (location gjk-player)) (+ spd)))
           (when (retained :c)
             (incf (vy (location gjk-player)) (- spd)))
           (when (retained :space)
             (incf (vy (location gjk-player)) (+ spd)))))
    (debug-clear)
    (setf (color gjk-player) #.(vec 0 1 0 0.5))
    (let ((hit (detect-hit* (test-method gjk-player) gjk-player (node :a (container gjk-player)) (hit gjk-player))))
      (when hit
        (setf (color gjk-player) #.(vec 1 0 0 0.5))
        (debug-line (hit-location hit) (v+* (hit-location hit) (hit-normal hit) 2)
                    :color-a #.(vec 0 0 0) :color-b #.(vec 0 1 0))))))

(define-example gjk
  :title "GJK Collision Detection"
  (enter (make-instance 'display-controller) scene)
  (enter (make-instance 'vertex-entity :vertex-array (// 'trial 'grid)) scene)
  (enter (make-instance 'gjk-body :name :a :primitive (make-box)) scene)
  (enter (make-instance 'gjk-player :name :b :primitive (make-sphere) :location (vec 0 0 +2.5)) scene)
  (enter (make-instance 'target-camera :location (vec3 0.0 8 9) :target (vec 0 0 0) :fov 50) scene)
  (observe! (hit-location (hit (node :b scene))) :title "Location")
  (observe! (hit-normal (hit (node :b scene))) :title "Normal")
  (observe! (hit-depth (hit (node :b scene))) :title "Depth")
  (enter (make-instance 'render-pass) scene))

(defmethod setup-ui ((scene gjk-scene) panel)
  (let ((layout (make-instance 'alloy:grid-layout :col-sizes '(T 120 140) :row-sizes '(30)))
        (focus (make-instance 'alloy:vertical-focus-list)))
    (flet ((shapes ()
             (list (make-sphere)
                   (make-box)
                   (make-cylinder)
                   (make-pill)
                   (make-plane)
                   (make-half-space))))
      (alloy:enter "Shape A" layout :row 0 :col 1)
      (alloy:represent (physics-primitive (node :a scene)) 'alloy:combo-set
                       :value-set (shapes) :layout-parent layout :focus-parent focus)
      (alloy:enter "Shape B" layout :row 1 :col 1)
      (alloy:represent (physics-primitive (node :b scene)) 'alloy:combo-set
                       :value-set (shapes) :layout-parent layout :focus-parent focus)
      (alloy:enter "Method" layout :row 2 :col 1)
      (alloy:represent (test-method (node :b scene)) 'alloy:combo-set
                       :value-set '(generic gjk v-clip) :layout-parent layout :focus-parent focus)
      (alloy:finish-structure panel layout focus))))
